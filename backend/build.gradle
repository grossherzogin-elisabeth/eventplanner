plugins {
    id "java"
    id "jacoco"
    id "org.springframework.boot" version "3.4.5"
    id "io.spring.dependency-management" version "1.1.7"
    id "io.freefair.lombok" version "8.13.1"
    id "net.ltgt.errorprone" version "4.2.0"
}

group = "org.eventplanner"
version = "1.0.0-SNAPSHOT"

java {
    sourceCompatibility = "21"
}

ext {
    springBootVersion = "3.4.5"
    hibernateVersion = "6.6.13.Final"
    apachePoiVersion = "5.4.1"
}

import net.ltgt.gradle.errorprone.CheckSeverity

tasks.withType(JavaCompile) {
    options.errorprone {
        check("NullAway", CheckSeverity.ERROR)
        option("NullAway:AnnotatedPackages", "com.uber")
    }
    // Include to disable NullAway on test code
    if (name.toLowerCase().contains("test")) {
        options.errorprone {
            disable("NullAway")
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // spring
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jdbc:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-freemarker:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-mail:${springBootVersion}"

    // sqlite
    implementation "org.xerial:sqlite-jdbc:3.49.1.0"
    implementation "org.hibernate.orm:hibernate-core:${hibernateVersion}"
    implementation "org.hibernate.orm:hibernate-community-dialects:${hibernateVersion}"
    implementation "org.flywaydb:flyway-core:11.8.2"

    // excel
    implementation "org.apache.poi:poi:${apachePoiVersion}"
    implementation "org.apache.poi:poi-ooxml:${apachePoiVersion}"

    // tests
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation "org.springframework.security:spring-security-test:6.4.5"

    // silence "unknown enum constant javax.annotation.meta.When.MAYBE" warning
    // https://stackoverflow.com/questions/53326271/spring-nullable-annotation-generates-unknown-enum-constant-warning
    // https://mvnrepository.com/artifact/com.google.code.findbugs/jsr305
    implementation "com.google.code.findbugs:jsr305:3.0.2"

    // monitoring
    runtimeOnly "io.micrometer:micrometer-registry-prometheus"

    // util
    implementation "org.apache.commons:commons-lang3:3.17.0"
    // https://mvnrepository.com/artifact/org.jspecify/jspecify
    implementation("org.jspecify:jspecify:1.0.0")

    // https://mvnrepository.com/artifact/com.google.errorprone/error_prone_core
    errorprone "com.google.errorprone:error_prone_core:2.38.0"

    errorprone "com.uber.nullaway:nullaway:0.12.7"
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}
