plugins {
    id "java"
    id "jacoco"
    id "org.springframework.boot" version "3.5.7"
    id "io.spring.dependency-management" version "1.1.7"
    id "io.freefair.lombok" version "9.0.0"
    id "org.sonarqube" version "7.0.1.6134"
}

group = "org.eventplanner"
version = "1.2.0-SNAPSHOT"

java {
    sourceCompatibility = "25"
}

ext {
    springBootVersion = "3.5.7"
    hibernateVersion = "6.6.13.Final"
    apachePoiVersion = "5.4.1"
}

repositories {
    mavenCentral()
}

dependencies {
    // spring
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jdbc:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-freemarker:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-mail:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"

    // sqlite
    implementation "org.xerial:sqlite-jdbc:3.50.3.0"
    implementation "org.hibernate.orm:hibernate-core:${hibernateVersion}"
    implementation "org.hibernate.orm:hibernate-community-dialects:${hibernateVersion}"
    implementation "org.flywaydb:flyway-core:11.15.0"

    // excel
    implementation "org.apache.poi:poi:${apachePoiVersion}"
    implementation "org.apache.poi:poi-ooxml:${apachePoiVersion}"

    // tests
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation "org.springframework.security:spring-security-test:6.5.6"
    testImplementation "com.tngtech.archunit:archunit:1.4.1"

    // silence "unknown enum constant javax.annotation.meta.When.MAYBE" warning
    // https://stackoverflow.com/questions/53326271/spring-nullable-annotation-generates-unknown-enum-constant-warning
    // https://mvnrepository.com/artifact/com.google.code.findbugs/jsr305
    implementation "com.google.code.findbugs:jsr305:3.0.2"

    // monitoring
    runtimeOnly "io.micrometer:micrometer-registry-prometheus"

    // util
    implementation "org.apache.commons:commons-lang3:3.19.0"
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    reports {
        xml.required = true
    }
}

sonar {
    properties {
        property "sonar.projectKey", "grossherzogin-elisabeth_eventplanner"
        property "sonar.organization", "grossherzogin-elisabeth"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.plugins.downloadOnlyRequired", true
        property "sonar.coverage.jacoco.xmlReportPaths", "backend/build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.sources", "src/main/java, ../frontend/src"
        property "sonar.tests", "src/test/java, ../frontend/test"
    }
}
